cmake_minimum_required(VERSION 3.19)

project(pentobi CXX)
set(PENTOBI_VERSION 22.0)
set(PENTOBI_RELEASE_DATE 2023-01-24)

include(GNUInstallDirs)

option(PENTOBI_BUILD_GTP "Build GTP interface" OFF)
option(PENTOBI_BUILD_GUI "Build GUI" ON)
option(BUILD_TESTING "Build tests" OFF)

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/")

if(NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
  message(STATUS "No build type selected, default to Release")
  set(CMAKE_BUILD_TYPE Release CACHE STRING "" FORCE)
endif()

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

if(BUILD_TESTING)
  enable_testing()
endif()

if(UNIX)
  add_custom_target(dist
    COMMAND git archive --prefix=pentobi-${PENTOBI_VERSION}/ HEAD
    | xz -e > ${CMAKE_BINARY_DIR}/pentobi-${PENTOBI_VERSION}.tar.xz
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR})
endif()

find_package(Threads)
add_subdirectory(libboardgame_base)
add_subdirectory(libboardgame_mcts)
add_subdirectory(libpentobi_base)
add_subdirectory(libpentobi_mcts)
if(BUILD_TESTING)
    add_subdirectory(libboardgame_test)
endif()
if(PENTOBI_BUILD_GTP)
    add_subdirectory(libboardgame_gtp)
    add_subdirectory(libpentobi_gtp)
    add_subdirectory(pentobi_gtp)
    if(UNIX)
        add_subdirectory(twogtp)
    else()
        message(STATUS "Not building twogtp, needs POSIX")
    endif()
    add_subdirectory(learn_tool)
endif()
if(PENTOBI_BUILD_GUI)
    set(CMAKE_AUTOMOC ON)
    set(CMAKE_AUTORCC ON)
    find_package(Gettext 0.19.6 REQUIRED)
    find_program(ITSTOOL itstool REQUIRED)
    find_program(RSVG_CONVERT rsvg-convert REQUIRED)
    if((UNIX AND NOT ANDROID))
        find_package(DocBookXSL REQUIRED)
        find_program(XSLTPROC xsltproc REQUIRED)
    endif()
    find_package(Qt6 6.2 REQUIRED
        COMPONENTS Concurrent LinguistTools QuickControls2)
    add_subdirectory(libpentobi_paint)
    add_subdirectory(pentobi)
    if(UNIX AND NOT ANDROID)
        add_subdirectory(pentobi_thumbnailer)
    endif()
endif()
